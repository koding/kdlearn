#!/usr/bin/env fish
#
# # Helper
#


if test (count $argv) -lt 1
  echo "
Helper command required. See the following command for help

    ./helper help
"
else
  switch $argv[1]
    case "help"
      echo "
Commands:
  build   # Build kdlearn
  deploy  # Deploy kdlearn
  init    # Use once to setup a new repo
  preview # Run a server to preview on port 8000

Example Deploy:
  ./helper build
  ./helper deploy
"
      exit 0

    case "build"
      cd ./build
      and set -l cur_br (git branch | sed -n 's/^\* //p')
      and test "$cur_br" = "production"
      and git reset --hard HEAD
      and git clean -fd
      and git pull origin production
      and cd ..
      and gulp
      or begin
        set -l err $status
        echo "Unknown Error. Exit status: $err, build branch: $cur_br"
        exit $err
      end

    case "deploy"
      cd ./build
      and git add .
      and git commit -m "updated site"
      and git push
      or begin
        set -l err $status
        echo "Unknown Error. Exit status: $err"
        exit $err
      end

    case "init"
      rm -rf ./build
      and git clone git@github.com:koding/kdlearn build
      and cd ./build
      and git checkout production
      and cd ..
      or begin
        set -l err $status
        echo "Unknown Error. Exit status: $err"
        exit $err
      end

    case "preview"
      cd ./build
      and python -m SimpleHTTPServer
  end
end


# vim: set filetype=fish:
